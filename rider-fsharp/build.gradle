buildscript {
    ext.kotlin_version = '1.1.3-2'

    FileTree tree = fileTree(dir: '../ReSharper.FSharp')
    tree.include 'JetBrains.ReSharper.FSharp.*.nupkg'
    ext.nupkg_path = tree.singleFile
    logger.warn("Using .nupkg from $nupkg_path")

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.2.7"
}

apply plugin: 'kotlin'

intellij {
    localPath "$projectDir/build/rider"
    type 'RS'
    downloadSources false
    updateSinceUntilBuild false
}

assert file(nupkg_path).exists()

prepareSandbox {
    from(nupkg_path, {
        into "$intellij.pluginName"
    })
}

runIde {
    systemProperties = [
            'rider.debug.mono.debug': true,
            'rider.debug.mono.allowConnect': true,
            'idea.platform.prefix' : "Rider"
    ]
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.4'
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group 'com.jetbrains'
